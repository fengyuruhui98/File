# 2. 临时变量的使用
## 2.1 变量的基本用法
命令：set key=value

**切记：key=value 三者之间绝对不能出现空格，不能为了像遵守 java 风格擅自给添加上空格，这与 java 的 int a = 1 声明变量不同，切记**

变量使用：%key%

示例：

@echo off
set name=dasuAndroidTv
echo %name%
set命令示例.png
效果：name 可当做临时变量使用，使用时需用 %% 将变量名括起来使用

局限：不允许在 for 命令中类似上步中声明变量并直接使用，如下：

```bat
@echo off
for /l %%i in (0,1,5) do (
    set name=dasuAndroidTv
    echo %name%
)```

效果：在 for 命令中声明临时变量，并直接通过 %key% 方式使用时会出现上图中的错误：ECHO 处于关闭状态，但如果 set key=value 临时变量的声明是放在 for 命令外部，for 命令内部只是使用的话，是可以的，如下：

@echo off
set name=dasuAndroidTv
for /l %%i in (0,1,5) do (
    echo %name%
)

效果：在 for 命令外部声明临时变量，for 命令内部只是使用，这种方式是允许的

## 2.2 变量在 for 命令中的用法
提问：那么如果要在 for 命令中才声明临时变量，并使用的话，该如何做？

for 命令中临时变量的使用：

需启用变量延迟功能，命令：setlocal enabledelayedexpansion
for 命令中的临时变量使用时用 !key! 感汉号括起来的形式代替 %key%
理由：不清楚，google 来的解决方案，感兴趣想深入研究的自行搜索
示例：
```bat
@echo off
setlocal enabledelayedexpansion
set name=dasu
for /l %%i in (0, 1, 5) do (
    set name=dasuAndroidTv 
    echo !name!
    echo %name%
)
```

效果：说得白一点，在 for 命令中通过 %name% 方式使用的临时变量，取的 name 这个临时变量的值会一直是它在 for 命令外赋值的内容，即使在 for 命令中通过 set 命令对这个变量又重新赋值，也不会生效。

那么，如果需要在 for 命令中通过 set 命令赋值后的临时变量能够马上拿来使用，需要两个步骤，一在文件开头启用变量延迟功能，命令：setlocal enabledelayedexpansion，二在 for 命令中通过 !name!方式来使用临时变量。

# 3. 字符串处理
## 3.1 截取
命令：%key:~[start,num]%

解释：当 %key% 中出现了 :~，则表示要对 key 指向的这个字符串做截取操作，截取操作支持以下几种形式：

截取指定位置开始的 n 个字符串：%key:~0,4%，表示截取从下标 0 开始的之后 4 个字符
截取从指定位置开始的整个字符串：%key:~4%，表示截取从下标为 4 开始的整个字符串
截取通过倒数方式指定开始位置的整个字符串：%key:~-2%，表示截取从倒数第 2 个字符开始的整个字符串
截取通过倒数方式指定位置开始之后的 n 个字符串：%key:-4,2%，表示截取从倒数第 4 个字符开始的 2 个字符
正数倒数方式相结合：%key:~2,-2%，表示截取从下标 2 开始到倒数第 2 个之间的字符串
示例：

@echo off
rem (rem表示后面是注释的内容，类似于 java //)原始字符串
set name=dasuAndroidTv

rem 注释内容：表示截取从下标 0 开始的之后 4 个字符，输出 dasu
echo %name:~0,4%

rem 注释内容：表示截取从下标为 4 开始的整个字符串，输出 AndroidTv
echo %name:~4%  

rem 注释内容：表示截取从倒数第 2 个字符开始的整个字符串，输出 Tv
echo %name:~-2%

rem 注释内容：表示截取从倒数第 4 个字符开始的 2 个字符，输出 Android
echo %name:~4,-2%

rem 注释内容：表示截取从下标 2 开始到倒数第 2 个之间的字符串，输出 id
echo %name:~-4,2%
字符串截取命令示例.png
## 3.2 拼接
命令：%key1%%key2%

解释：将要拼接的那个字符串直接跟在被拼接的后面即可，不需要任何拼接操作符

示例：

@echo off
set name1=dasu
set name2=AndroidTv
echo %name1%%name2%  
rem 这里是注释内容：输出 dasuAndroidTv
字符串截取命令示例2.png
## 3.3 替换
命令：%key:被替换字符串=替换的字符串%

解释：不解释了，直接看示例，很容易明白

示例：

@echo off
set name=whoAndroidTv
echo %name:who=dasu%
rem 这里是注释内容：输出 dasuAndroid
字符串替换命令示例.png
## 3.4 文件特殊操作
如果是在 for 命令中遍历了某个文件夹下的文件，那么此时可以通过一些特殊命令来获取这个文件的各种信息，直接看示例：

@echo off
for %%i in (*.txt) do (
   echo %%i
   echo %%~fi
   echo %%~di
   echo %%~pi
   echo %%~ni
   echo %%~xi
   
   echo %%~ti
   echo %%~zi
)
特殊命令示例.png
解释： 在通过 for 命令遍历文件时，%%i 根据不同的 for 使用方式，内容也有所不同，具体见第 1 节。在上述这种用法下，%%i 指向了当前目录下每个文件名，完整的文件名。

那么，此时就可以通过一些特殊命令来取得文件的相关信息，比如：

%%~fi：表示获取该文件的绝对路径信息
%%~di：表示获取该文件所在的盘符
%%~pi：表示获取该文件的路径，不包含盘符的信息
%%~ni：表示获取该文件的文件名，不包含扩展名信息
%%~xi：表示获取该文件的扩展名
%%~ti：表示获取该文件的上次修改时间
%%~zi：表示获取该文件的大小
# 4. 完整示例
最后，我们来个具体场景，将本篇所学的知识用上一遍，巩固一下。

场景：遍历指定路径目录下的所有 apk 文件，并通过一个 sign.jar 文件，分别对每个 apk 文件执行 java 命令来进行签名工作，sign.jar 接收两个参数，一个是需要签名的 apk，另外一个为输出的 apk，要求签名后的 apk 命名方式为将原文件名中的 unsign 替换成 google，并输出在跟 apk 同一个目录内即可。

apk 路径：c:\users\suxq\desktop\outputs

sign.jar 路径：c:\users\suxq\desktop

java 签名命令示例(要求 sign.jar 和 apk 文件都在同一路径下，即可用如下命令)：

java -jar sign.jar meizi_1_3_0_debug_unsign.apk meizi_1_3_0_debug_google.apk

批处理脚本代码：

```bat
@echo off
setlocal enabledelayedexpansion

set sign=c:\users\suxq\desktop\sign.jar
set apkPath=c:\users\suxq\desktop\outputs\

for %%i in (%apkPath%*.apk) do (
    set oldApk=%%~nxi
    set outApk=!oldApk:unsign=google!
    echo java -jar %sign% !oldApk! !outApk!
    rem 这里是注释内容：由于 apk 文件 和 sign.jar 文件都是虚拟的，因此真正执行时会报错，这里就只是将 java 整句命令输出，从整句命令就可以确认是否会正确执行，如果这些文件都是真的话。真的脚本应该将 echo 去掉
)
```

文件处理的另一个例子
```bat
// 作用：批处理批量读取目录中文件，并用for循环对文件逐一进行处理。
// 注：.bat文件中的注释符是::，此处为了显示分明，使用//代替。
@echo off
set input_path=E:\CZJ_coded\1280_640

// 方法一：先将input_path路径中的包含Kite和1280x640的文件名存入name.txt中，再使用for循环读
dir %input_path%\Kite*1280x640*.264 /b/od>%input_path%\name.txt

//~ni的作用是去掉文件名中的格式后缀，例如 %%i 是aa.264，则%%~ni就是aa。
for /F %%i in (%input_path%\name.txt) do (
ffmpeg -i %input_path%\%%i -vcodec copy %input_path%\%%~ni.ts )


//方法二,省略dir的步骤
::for /r %input_path% %%i in (*.264) do (
ffmpeg -i %%i %input_path%\%%~ni.ts )

del %input_path%\*.mp4

```